//    cc2600 - a subset of C compiler for the Atari 2600
//    Copyright (C) 2023 Bruno STEUX 
//
//    This program is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    This program is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with this program.  If not, see <https://www.gnu.org/licenses/>.
//
//    Contact info: bruno.steux@gmail.com

WHITESPACE   =  _{ " " | "\t" | NEWLINE }
COMMENT   = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE } 

program      =   { SOI ~ decl* ~ EOI }

decl = { var_decl
       | func_decl 
       | included_assembler
       }
var_decl = { var_type ~ id_name_ex ~ ("," ~ id_name_ex)* ~ ";"+ }
var_type = { (var_const | superchip | bank | aligned | display | frequency)* ~ var_sign ~ var_simple_type }
var_const = { "const" }
superchip = { "superchip" }
display = { "display" }
frequency = { "frequency" }
aligned = { "aligned" ~ "(" ~ int ~ ")" }
var_sign = { ("signed" | "unsigned")? } 
var_simple_type = { "char" | "short" }
id_name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
id_name_ex = { pointer? ~ var_const? ~ id_name ~ array_spec? ~ ("=" ~ var_def)? }
array_spec = { "[" ~ int? ~ "]" }
var_def = { int | array_def | quoted_string }
array_def = { "{" ~ (int ~ ",")* ~ int ~ "}" }
pointer = { "*" }

func_decl = { inline? ~ bank? ~ "void" ~ id_name ~ "(" ~ ("void")? ~ ")" ~ (";" | block) ~ ";"*}
inline = { "inline" }
bank = ${ "bank" ~ bank_number }
bank_number = { ASCII_NONZERO_DIGIT }

included_assembler = ${ "=== ASSEMBLER BEGIN ===" ~ assembler_code ~ "==== ASSEMBLER END ====" }
assembler_code = ${(!"==== ASSEMBLER END ===="~ ANY)*} 

block = { "{" ~ statement* ~ "}" }
label = { id_name ~ ":" }
statement = { label? ~
    ( block
    | do_while
    | while_do
    | for_loop
    | if_statement
    | switch_statement
    | break_statement
    | continue_statement
    | return_statement
    | expr ~ ";"
    | nothing ~ ";"
    | asm_statement
    | strobe_statement
    | load_statement
    | store_statement
    | csleep_statement
    | goto_statement
    ) }
do_while = { "do" ~ statement ~ "while" ~ "(" ~ expr ~ ")" ~ ";" }
while_do = { "while" ~ "(" ~ expr ~ ")"~ statement }
for_loop = { "for" ~ "(" ~ (expr | nothing) ~ ";" ~ (expr | nothing) ~ ";" ~ (expr | nothing) ~ ")" ~ statement }
if_statement = { "if" ~ "(" ~ expr ~ ")" ~ statement ~ ("else" ~ statement)* }
switch_statement = { "switch" ~ "(" ~ expr ~ ")" ~ "{" ~ cases ~ "}" }
break_statement = { "break" ~ ";" }
continue_statement = { "continue" ~ ";"}
return_statement = { "return" ~ ";"}
asm_statement = { "asm" ~ "(" ~ quoted_string ~ ")" ~ ";"}
load_statement = { "load" ~ "(" ~ expr ~ ")" ~ ";"}
store_statement = { "store" ~ "(" ~ expr ~ ")" ~ ";"}
strobe_statement = { "strobe" ~ "(" ~ expr ~ ")" ~ ";"}
csleep_statement = { "csleep" ~ "(" ~ int ~ ")" ~ ";"}
goto_statement = { "goto" ~ id_name ~ ";"}

nothing = { "" }

cases = { ("case" ~ int ~ ":" ~ ((!"default" ~ statement)*))* ~ default_case? }
default_case = { "default" ~ ":" ~ statement* } 

expr       =   { prefix* ~ primary ~ postfix* ~ (infix ~ prefix* ~ primary ~ postfix* )* }
    infix    =  _{ pass | mass | mulass | divass | blsass | brsass | andass | xorass | orass |
                   mul | div | add | sub | bls | brs | lte | lt | gte | gt | eq | neq |
                   land | lor | and | xor | or | assign | comma | ternary_cond1 | ternary_cond2
                   }
      mul    =   { "*" }  // Multiplication
      div    =   { "/" }  // Division
      add    =   { "+" }  // Addition
      sub    =   { "-" }  // Subtraction
      bls    =   { "<<" } // Bitwise left shift
      brs    =   { ">>" } // Bitwise right shift
      lt     =   { "<" }  // Less than
      lte    =   { "<=" } // Less than or equal
      gt     =   { ">" }  // Greater than
      gte    =   { ">=" } // Greater than or equal
      eq     =   { "==" } // Equal
      neq    =   { "!=" } // Different
      and    =   { "&" }  // And
      xor    =   { "^" }  // Xor
      or     =   { "|" }  // Or
      land   =   { "&&" } // Logical and
      lor    =   { "||" } // Logical or
      assign =   { "=" }  // Assignment
      pass   =   { "+=" } // Add assign
      mass   =   { "-=" } // Sub assign
      mulass =   { "*=" } // Multiply assign
      divass =   { "/=" } // Divide assign
      blsass =   { "<<=" }// Bitwise left shift assign
      brsass =   { ">>=" }// Bitwise right shift assign
      andass =   { "&=" } // And assign
      xorass =   { "^=" } // Or assign
      orass  =   { "|=" } // Or assign
      comma  =   { "," }  // Comma (discard left)
      ternary_cond1 = { "?" } // Ternary conditional (condition part)
      ternary_cond2 = { ":" } // Ternary conditional (alternatives part)
    prefix   =  _{ neg | ppp | mmp | addr | deref | not | bnot }
      ppp    =   { "++" } // Increment
      mmp    =   { "--" } // Decrement
      neg    =   { "-" }  // Negation
      not    =   { "!" }  // Not
      bnot   =   { "~" }  // Bitwise not
      addr   =   { "&" }  // Adress of
      deref  =   { "*" }  // Dereference
    postfix  =  _{ pp | mm | call }
      pp     =   { "++" } // Increment
      mm     =   { "--" } // Decrement
      call   =   { "()" } // Function call
    primary  =  _{ int | identifier | "(" ~ expr ~ ")" }

identifier  = { id_name ~ subscript? }
subscript   = { "[" ~ expr ~ "]" }
      
int = { hexadecimal | octal | decimal | quoted_character }
decimal     = @{ ("-")* ~ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }
hexadecimal = @{ "0x" ~ ASCII_HEX_DIGIT+ }
octal       = @{ "0" ~ ASCII_OCT_DIGIT+ }
character   = @{ ANY }
quoted_character   = ${ "'" ~ character ~ "'" }
string      = @{ (!"\"" ~ character)* } 
quoted_string      = ${ "\"" ~ string ~ "\""}
